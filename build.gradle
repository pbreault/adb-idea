buildscript {

    ext.kotlin_version = '1.1.3'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.9"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.15"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8



apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'


repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        resources {
            exclude 'website/adb_operations_popup.png'
            exclude 'website/find_actions.png'
            exclude 'website/debug_howto.png'
        }
    }
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

intellij {
    pluginName 'adb_idea'
    updateSinceUntilBuild false

    intellij.localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
}

group 'com.developerphil.intellij.plugin.adbidea'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compile "org.jetbrains.kotlin:kotlin-runtime:${kotlin_version}"
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
    compile 'com.google.dagger:dagger:2.6'

    kapt "com.google.dagger:dagger-compiler:2.6"

    testCompile 'junit:junit:4.12'
    testCompile fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testCompile fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
    testCompile "org.mockito:mockito-core:1.+"
    testCompile "com.google.truth:truth:0.30"

}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}



compileJava.dependsOn verifySetup
