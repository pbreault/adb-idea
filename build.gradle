
plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'java'

sourceSets {
    main {
        resources {
            exclude 'website/adb_operations_popup.png'
            exclude 'website/find_actions.png'
            exclude 'website/debug_howto.png'
        }
    }
}

intellij {
    version 'IC-15.0.5' // this value is not important, as long as you can compile

    pluginName 'adb_idea'
    updateSinceUntilBuild false

    intellij.alternativeIdePath = AndroidStudioPath

}

group 'com.developerphil.intellij.plugin.adbidea'
def majorVersion = '1.3.0-experimental'

dependencies {
    compileOnly fileTree(dir: "$AndroidStudioPath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$AndroidStudioPath/lib", include: ['*.jar'])

}

configurations {
    compile.exclude module:'idea'
}


task(verifySetup) << {

    def ideaJar = "$AndroidStudioPath/lib/idea.jar"
    if ( ! file(ideaJar).exists() ) {
        throw new GradleException("$ideaJar not found, set AndroidStudioPath in gradle.properties")
    }
}

compileJava.dependsOn verifySetup
