buildscript {

    ext.kotlin_version = '1.3.41'

    repositories {
        maven {url 'https://maven.aliyun.com/repository/public'}
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.2"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8



apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'


repositories {
    maven {url 'https://maven.aliyun.com/repository/public'}
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        resources {
            exclude 'website/*.png'
        }
    }
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

intellij {
    pluginName 'adb_idea'
    updateSinceUntilBuild false

    intellij.localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
}

group 'com.developerphil.intellij.plugin.adbidea'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
    compile "org.jooq:joor-java-8:0.9.7"
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testImplementation fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation "com.google.truth:truth:1.0.1"


}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}


compileJava.dependsOn verifySetup