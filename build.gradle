plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.21"
    id "org.jetbrains.intellij" version "1.2.0"
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    maven { url 'https://maven.aliyun.com/repository/google' }
    mavenCentral()
    jcenter()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8




sourceSets {
    main {
        resources {
            exclude 'website/*.png'
        }
    }
}

if (!hasProperty('StudioCompilePath')) {
    throw new GradleException("No StudioCompilePath value was set, please create gradle.properties file")
}

intellij {
    pluginName = 'adb_idea'
    updateSinceUntilBuild = false
    localPath = project.hasProperty("StudioRunPath") ? StudioRunPath : StudioCompilePath
    instrumentCode {
        compilerVersion = "212.4746.92"
    }
    instrumentTestCode {
        compilerVersion = "212.4746.92"
    }

}


group 'com.developerphil.intellij.plugin.adbidea'

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'
    implementation 'org.jooq:joor-java-8:0.9.14'
    compileOnly fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    compileOnly fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])

    testImplementation 'junit:junit:4.13.2'
    testImplementation fileTree(dir: "$StudioCompilePath/plugins/android/lib", include: ['*.jar'])
    testImplementation fileTree(dir: "$StudioCompilePath/lib", include: ['*.jar'])
    testImplementation "org.mockito:mockito-core:1.+"
    testImplementation 'com.google.truth:truth:1.1.3'


}

task(verifySetup) {
    doLast {
        def ideaJar = "$StudioCompilePath/lib/idea.jar"
        if (!file(ideaJar).exists()) {
            throw new GradleException("$ideaJar not found, set StudioCompilePath in gradle.properties")
        }
    }
}


compileJava.dependsOn verifySetup
buildSearchableOptions.enabled = false